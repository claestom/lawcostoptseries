// This query will look for the amount of times tables in the workspace have been queries by any service or user in the specified time interval

let TimeRange = 7d;
// First: get all tables with data in the time range
let TablesWithData = 
    union withsource = Tables *
    | where TimeGenerated > ago(TimeRange)
    | distinct Tables;

// Second: get counts of tables referenced in queries in the time range
let QueriedTables = 
    LAQueryLogs
    | where TimeGenerated > ago(TimeRange)
    | extend SourceTable = extract(@"(\w+)\s*(\||$)", 1, QueryText)
    | summarize Count = count() by SourceTable;

// Merge: fullouter join and fill missing counts with 0, filter, and sort
TablesWithData
| join kind=fullouter (QueriedTables) on $left.Tables == $right.SourceTable
| extend TableName = coalesce(Tables, SourceTable)
| extend TableCount = coalesce(Count, 0)
| where isnotempty(TableName) and TableName !in ("table", "Tables", "Operation")
| project TableName, TableCount
| sort by TableCount desc